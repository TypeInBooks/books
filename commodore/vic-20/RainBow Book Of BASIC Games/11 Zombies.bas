100 REM ZOMBIES
110 GOSUB 1100
120 PRINT "{space*6}zombies"
130 LET T=2:GOSUB 1400:GOSUB 2800
140 PRINT:PRINT " ******************"
150 FOR I=1 TO 13:PRINT " *{space*16}*":NEXT I
160 PRINT " ******************"
170 DIM Q(8,2)
180 DIM Z(4,2)
190 FOR I=1 TO 4:LET N=16:GOSUB 1300:LET Z(I,1)=R+1:LET N=13:GOSUB 1300:LET Z(I,2)=R+1
200 LET X=Z(I,1):LET Y=Z(I,2):GOSUB 1700:PRINT "z";
210 NEXT I
220 FOR I=1 TO 8:LET N=16:GOSUB 1300:LET Q(I,1)=R+1:LET N=13:GOSUB 1300:LET Q(I,2)=R+1
230 LET X=Q(I,1):LET Y=Q(I,2):GOSUB 1700:PRINT "*";
240 NEXT I
250 LET N=16:GOSUB 1300:LET A=R+1:LET N=13:GOSUB 1300:LET B=R+1:LET X=A:LET Y=B
260 GOSUB 1700:PRINT "h";:LET N=4
270 GOSUB 1500:GOSUB 3100:LET G=GG-48:IF G<1 OR G>9 THEN GOTO 270
280 LET X=A:LET Y=B:GOSUB 1700:PRINT ".";
290 LET D=INT((G-4)/3):LET C=G-3*D-5:LET A=A+C:LET B=B+D
300 IF A=1 OR A=18 OR B=1 OR B=15 THEN GOTO 320
310 GOTO 330
320 LET M$="you're in the swamp":GOTO 480
330 LET F=0:FOR I=1 TO 8:IF A=Q(I,1) AND B=Q(I,2) THEN LET F=1
340 NEXT I:IF F=1 THEN LET M$="you're in quicksand":IF F=1 THEN GOTO 480
350 FOR I=1 TO 4:IF A=Z(I,1) AND B=Z(I,2) THEN LET F=1
360 NEXT I:IF F=1 THEN LET M$="you're caught!":IF F=1 THEN GOTO 480
370 LET X=A:LET Y=B:GOSUB 1700:PRINT "h";:LET Z=50:GOSUB 2400:LET T=0.5:GOSUB 1400
380 FOR I=1 TO 4:IF Z(I,1)=0 THEN GOTO 460
390 LET T=0.5:GOSUB 1400:LET X=Z(I,1):LET Y=Z(I,2):GOSUB 1700:PRINT ".";:LET F=0
400 LET X=X+SGN(A-X):LET Y=Y+SGN(B-Y):LET F=0:FOR J=1 TO 8:IF X=Q(J,1) AND Y=Q(J,2) THEN LET F=1
410 NEXT J:IF F<>1 THEN GOTO 430
420 LET N=N-1:LET Z(I,1)=0:LET Z=35:GOSUB 2400:GOTO 460
430 GOSUB 1700:PRINT "z":LET Z(I,1)=X:LET Z(I,2)=Y:LET Z=20:GOSUB 2400
440 IF X=A AND Y=B THEN LET M$="caught!":GOTO 480
450 GOSUB 1700:PRINT "z":LET Z(I,1)=X:LET Z(I,2)=Y
460 NEXT I:IF N>0 THEN GOTO 270
470 LET M$="you've made it !"
480 LET X=1:LET Y=16:GOSUB 1700:PRINT M$;
490 LET T=4:GOSUB 1400:GOSUB 2800:PRINT:PRINT:STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
